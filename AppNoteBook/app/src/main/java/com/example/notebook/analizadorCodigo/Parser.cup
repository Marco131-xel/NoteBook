package com.example.notebook.analizadorCodigo;

import java_cup.runtime.Symbol;
import java.util.ArrayList;
import java.util.LinkedList;
import java.util.List;
import com.example.notebook.abstracto.Instruccion;
import com.example.notebook.excepciones.Errores;
import com.example.notebook.simbolo.*;
import com.example.notebook.expresiones.*;
import com.example.notebook.instrucciones.*;
import com.example.notebook.reportes.*;

// CODIGO USUARIO
parser code {:
    Lexer s;
    parser(Lexer s) {
        this.s =s;
        this.listaErrores = s.listaErrores;
    }

    public LinkedList<Errores> listaErrores = new LinkedList<>();
    public List<Ocurrencia> ocurrencias = new ArrayList<>();
    public List<Ocurrencia> historialOcurrencias = new ArrayList<>();

    public void syntax_error(Symbol s){
        System.out.println("Error sintactico detectado: " + s.value + " en linea " + s.left + " columna " + s.right);
        listaErrores.add(new Errores("SINTACTICO",
                        "No se esperaba el componente "+s.value,
                        s.left,
                        s.right));
    }

    public void unrecovered_syntax_error (Symbol s){
        System.out.println("Error no recuperable: " + s.value + " en linea " + s.left + " columna " + s.right);
        listaErrores.add(new Errores("SINTACTICO NO RECUPERABLE",
                        "No se esperaba el componente "+s.value,
                        s.left,
                        s.right));
    }

    public List<Ocurrencia> getOcurrencia(){
        return ocurrencias;
    }

    public void registrarOcurrencia(String operador, int linea, int columna, String expresion) {
        Ocurrencia nueva = new Ocurrencia(operador, linea, columna, expresion);
        ocurrencias.add(nueva);
        historialOcurrencias.add(nueva);
    }

    public List<Ocurrencia> getHistorialOcurrencias(){
        return historialOcurrencias;
    }

    public parser getParser() {
    return this;
    }

:};

action code {::};

// TERMINALES
terminal MAS, MENOS, POR, DIV, PAR1, PAR2, UMENOS;
terminal POTENCIA;
terminal IGUAL, COMA, PUNTO;
terminal PRINT, FORMAT, PLOT, REPORTE, ERRORES, OPERADORES;
terminal String CADENA, ENTERO, DECIMAL, IDENTIFICADOR;
// NO TERMINALES
non terminal LinkedList<Instruccion>INICIO, INSTRUCCIONES;
non terminal Instruccion INSTRUCCION;
non terminal Instruccion IMPRESION, ASIGNARDECLARACION;
non terminal Instruccion EXPRESION;
non terminal Instruccion FORMATEAR, PLOTEAR;
non terminal Instruccion REPOERROR, REPOPE;
// PRECEDENCIA
precedence left MAS, MENOS;
precedence left POR, DIV;
precedence right POTENCIA;
precedence left UMENOS;

// PRODUCCIONES Y GRAMATICAS
start with INICIO;

INICIO ::= INSTRUCCIONES:a {: RESULT = a; :};

INSTRUCCIONES ::= INSTRUCCION:a                 {: RESULT = new LinkedList<>(); RESULT.add(a); :}
                | INSTRUCCIONES:a INSTRUCCION:b {: RESULT = a; RESULT.add(b); :}
;

INSTRUCCION ::= IMPRESION:a {: RESULT = a; :}
              | ASIGNARDECLARACION:a {: RESULT = a; :}
              | FORMATEAR:a {: RESULT = a; :}
              | PLOTEAR:a {: RESULT = a; :}
              | REPOERROR:a {: RESULT = a; :}
              | REPOPE:a {: RESULT = a; :}
              | error {: System.out.println("Recuperacion de error sintactico en linea "); :}
;

IMPRESION ::= PRINT PAR1 EXPRESION:a PAR2 {: RESULT = new Print(a, aleft, aright); :}
;

ASIGNARDECLARACION ::= IDENTIFICADOR:a IGUAL EXPRESION:b  {: RESULT = new AsignacionODeclaracion(a, b, aleft, aright); :}
;

FORMATEAR ::= FORMAT PAR1 EXPRESION:a PAR2 {: RESULT = new Format(a, aleft, aright); :}
;

PLOTEAR ::= PLOT PAR1 EXPRESION:a COMA EXPRESION:b COMA EXPRESION:c PAR2 {: RESULT = new Plot(a, b, c, aleft, aright); :}
;

REPOERROR ::= REPORTE PUNTO ERRORES:p PAR1 PAR2 {: RESULT = new RepoErrores(pleft, pright); :}
;

REPOPE ::= REPORTE PUNTO OPERADORES:p PAR1 PAR2 {: RESULT = new RepoOcurrencias(((parser) getParser()), pleft, pright); :}
;

EXPRESION ::= MENOS EXPRESION:a {: RESULT = new Aritmeticas(a, OperadoresAritmeticos.NEGACION, aleft, aright); :} %prec UMENOS
            | EXPRESION:a MAS EXPRESION:b   {: RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.SUMA, aleft, aright); String expresion = a + " + " + b; registrarOcurrencia("Suma", aleft, aright, expresion); :}
            | EXPRESION:a MENOS EXPRESION:b {: RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.RESTA, aleft, aright); String expresion = a+" - " +b; registrarOcurrencia("Resta", aleft, aright, expresion); :}
            | EXPRESION:a POR EXPRESION:b   {: RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.MULTIPLICACION, aleft, aright); String expresion = a+" * "+b; registrarOcurrencia("Multiplicacion", aleft, aright, expresion); :}
            | EXPRESION:a DIV EXPRESION:b   {: RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.DIVISION, aleft, aright); String expresion = a+" / "+b; registrarOcurrencia("Division", aleft, aright, expresion); :}
            | EXPRESION:a POTENCIA EXPRESION:b   {: RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.POTENCIA, aleft, aright); String expresion = a+" ^ "+b; registrarOcurrencia("Potencia", aleft, aright, expresion); :}
            | ENTERO:a          {: RESULT = new Nativo(Integer.parseInt(a), new Tipo(TipoDato.ENTERO), aleft, aright ); :}
            | DECIMAL:a         {: RESULT = new Nativo(new Double(a), new Tipo(TipoDato.DECIMAL), aleft, aright ); :}
            | CADENA:a          {: RESULT = new Nativo(a, new Tipo(TipoDato.CADENA), aleft, aright ); :}
            | IDENTIFICADOR:a   {: RESULT = new AccesoVar(a, aleft, aright); :}
            | PAR1 EXPRESION:a PAR2 {: RESULT = a; :}
;
